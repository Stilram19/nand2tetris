/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // zx
        Mux16(a=x, b=false, sel=zx, out=x0);

        // nx
        Not16(in=x0, out=notX0);
        Mux16(a=x0, b=notX0, sel=nx, out=x1);

        // zy
        Mux16(a=y, b=false, sel=zy, out=y0);

        // ny
        Not16(in=y0, out=notY0);
        Mux16(a=y0, b=notY0, sel=ny, out=y1);

        // f
        Add16(a=x1, b=y1, out=X1plusY1);
        And16(a=x1, b=y1, out=X1andY1);
        Mux16(a=X1andY1, b=X1plusY1, sel=f, out=preout);

        // no
        Not16(in=preout, out=notPreOut);
        Mux16(a=preout, b=notPreOut, sel=no, out=tempOut);

        // zr
        And16(a=true, b=tempOut, out[0..7]=out0);
        And16(a=true, b=tempOut, out[8..15]=out1);
        Or8Way(in=out0, out=zr0);
        Or8Way(in=out1, out=zr1);
        Or(a=zr0, b=zr1, out=notZr);
        Not(in=notZr, out=zr);

        // ng
        And16(a=true, b=tempOut, out[15]=ng);

        // out
        And16(a=true, b=tempOut, out=out);
}
